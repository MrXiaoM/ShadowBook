plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group = "top.mrxiaom"
version = "1.0.0"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://mvn.lumine.io/repository/maven/" }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0-SNAPSHOT'

    implementation "de.tr7zw:item-nbt-api:2.12.0"
    implementation "com.zaxxer:HikariCP:4.0.3"
    implementation "com.github.MrXiaoM:SQLHelper:e49785e161"
    implementation "commons-io:commons-io:2.15.0"
    implementation "commons-lang:commons-lang:2.6"
    implementation "org.jetbrains:annotations:19.0.0"
}
shadowJar {
    classifier("")
    def targetGroup = "top.mrxiaom.fix.book.utils."
    relocate("org.intellij.lang.annotations", targetGroup + "annotations.intellij")
    relocate("org.jetbrains.annotations", targetGroup + "annotations.jetbrains")
    relocate("top.mrxiaom.sqlhelper", targetGroup + "sql")
    relocate("org.apache.commons", targetGroup + "commons")
    relocate("com.zaxxer.hikari", targetGroup + "hikari")
    relocate("de.tr7zw.changeme.nbtapi", targetGroup + "nbtapi")
}
tasks.build.dependsOn(shadowJar)

def targetJavaVersion = 8
java {

    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand(
            "name": rootProject.name,
            "version": version,
        )
        include "plugin.yml"
    }
}
publishing {
    publications {
        mavenRelease(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.name
            version = project.version
        }
    }
}
